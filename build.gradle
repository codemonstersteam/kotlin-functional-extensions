plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'org.jetbrains.kotlin.jvm' version '1.6.10'
    id 'org.jetbrains.dokka' version "1.6.10"
}

group = 'team.codemonsters'
version = '0.0.2'
repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    implementation platform('org.jetbrains.kotlin:kotlin-bom')
    implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8")
    testImplementation("org.jetbrains.kotlin:kotlin-test")
    testImplementation("org.assertj:assertj-guava:3.4.0")
}

test {
    useJUnitPlatform()
}

tasks.named('jar') {
    manifest {
        attributes('Implementation-Title': rootProject.name,
                'Implementation-Version': project.version)
    }
}

/**
 * PUBLISHING SECTION
 */
java {
    withJavadocJar()
    withSourcesJar()
}

def sonatypeUsername = findProperty("ossrhUsername")
def sonatypePassword = findProperty("ossrhPassword")

publishing {
    repositories {
        maven {
            name = "oss"
            def releasesRepoUrl = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://oss1.sonatype.org/content/repositories/snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username = sonatypeUsername
                password = sonatypePassword
            }
        }
    }
    publications {
        mavenJava(MavenPublication) {

            pom {
                name = 'kotlin-functional-extensions'
                description = 'This library helps write code in more functional way.'
                url = 'https://github.com/codemonstersteam/kotlin-functional-extensions/'

                licenses {
                    license {
                        name = 'GNU GENERAL PUBLIC LICENSE, Version 3'
                        url = 'https://www.gnu.org/licenses/gpl-3.0.html'
                    }
                }
                developers {
                    developer {
                        id = 'max0l0gy'
                        name = 'Maxim Morev'
                        email = 'maxim@codemonsters.team'
                        url = 'https://github.com/max0l0gy'
                    }
                }
                scm {
                    connection = 'scm:git@github.com:codemonstersteam/kotlin-functional-extensions.git'
                    developerConnection = 'scm:git@github.com:codemonstersteam/kotlin-functional-extensions.git'
                    url = 'https://github.com/codemonstersteam/kotlin-functional-extensions'
                }
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}
